plugins {
    id 'com.android.application'
}

android {
    namespace 'com.infisense.usbir'
    compileSdk rootProject.ext.android.compileSdkVer

    defaultConfig {
        applicationId 'com.infisense.usbir'
        minSdk rootProject.ext.android.minSdkVer
        targetSdk rootProject.ext.android.targetSdkVer
        versionCode rootProject.ext.android.versionCodeUSBIR
        versionName "${rootProject.ext.android.versionNameUSBIR}${new Date().format('.yyMMddHH', TimeZone.getTimeZone('GMT+08:00'))}${isReleaseVersion.toBoolean() ? '' : debugVersionInfo}"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        ndk {
            abiFilters 'arm64-v8a', 'armeabi', 'armeabi-v7a'
        }
        resValue 'bool', 'isReleaseVersion', isReleaseVersion
        resValue 'string', 'debugVersionInfo', debugVersionInfo
        buildConfigField 'boolean', 'isStandardSDK', "${isStandardSDK}"
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles(getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro')
        }
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles(getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro')
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    ndkVersion rootProject.ext.android.ndkVersion

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "USB_IR_${new Date().format('yyMMddHHmm', TimeZone.getTimeZone('GMT+08:00'))}${isReleaseVersion.toBoolean() ? '' : debugVersionInfo}${isStandardSDK.toBoolean() ? '_STANDARD' : '_CUSTOM'}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation roots.appcompat
    implementation roots.material
    implementation roots.constraintlayout
    implementation roots['kotlin']
    implementation thirdFrame['fastjson']
    releaseImplementation thirdFrame['dokitx-no-op']
    debugImplementation thirdFrame['dokitx']

    testImplementation roots.junit
    androidTestImplementation roots['ext-junit']
    androidTestImplementation roots['espresso-core']
}
